buildscript {
    ext.kotlin_version = '1.2.51'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'idea'
apply plugin: 'application'
mainClassName = "uk.co.nickthecoder.paratask.ParaTaskAppKt"

defaultTasks 'install', 'installDist'

dependencies {
    compile project(':paratask-core')
    compile project(':paratask-app')
    // Comment out the following line, and also the line in settings.gradle to compile paratask without jediterm.
    compile project(':paratask-jediterm')
}

task generateCompletion(type: Exec) {
    executable = 'build/install/paratask/bin/paratask'
    args = ['generateCompletion']
}
distTar.dependsOn generateCompletion
distZip.dependsOn generateCompletion
run.dependsOn generateCompletion

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    flatDir dirs: ['jediterm']
}

subprojects {

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        flatDir dirs: ['../jediterm']
    }

}

project(':paratask-core') {

    apply plugin: 'kotlin'
    apply plugin: 'maven'

    version = '0.2'
    group = 'uk.co.nickthecoder'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }

    defaultTasks 'install'
}

project(':paratask-app') {

    apply plugin: 'kotlin'

    dependencies {
        compile project(':paratask-core')

        compile 'org.codehaus.groovy:groovy-all:2.4.9'
        compile 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
        compile 'uk.co.nickthecoder:tedi-core:0.1'
        compile 'mysql:mysql-connector-java:5.1.6'

        testCompile 'junit:junit:4.11'
        testCompile group: 'org.loadui', name: 'testFx', version: '3.1.0'
    }

    defaultTasks 'jar'

}

project(':paratask-examples') {
    apply plugin: 'kotlin'

    dependencies {
        compile project(':paratask-app')
    }
}

project(':paratask-jediterm') {
    apply plugin: 'kotlin'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile project(':paratask-app')
        compile project(':paratask-core')

        compile ':jediterm-pty:2.2'
        compile ':jediterm-ssh:2.2'
        compile ':jediterm-ssh-connector:2.2'
        compile ':pty4j:0.7.2'
        compile ':purejavacomm:0.0.17'

        compile 'com.github.purejavacomm:purejavacomm:1.0.2.RELEASE'
        compile 'net.java.dev.jna:jna:4.2.2'
        compile 'net.java.dev.jna:jna-platform:4.2.2'
        compile 'com.intellij:javac2:7.0.3'
        compile 'com.intellij:annotations:12.0'
        compile 'com.google.guava:guava:14.0.1'
        compile 'com.jcraft:jsch:0.1.51'
        compile 'log4j:log4j:1.2.14'
        compile 'com.jcraft:jzlib:1.1.1'
    }

    defaultTasks 'jar'
}
